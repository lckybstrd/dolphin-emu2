# This is included by CPack (CPACK_PROJECT_CONFIG_FILE) during package generation

set(PROJECT_SOURCE_DIR "@PROJECT_SOURCE_DIR@")
set(DISTRIBUTOR "@DISTRIBUTOR@")
set(DOLPHIN_DEFAULT_UPDATE_TRACK "@DOLPHIN_DEFAULT_UPDATE_TRACK@")
set(GIT_FOUND "@GIT_FOUND@")
set(GIT_EXECUTABLE "@GIT_EXECUTABLE@")
set(DOLPHIN_WC_REVISION "@DOLPHIN_WC_REVISION@")
set(DOLPHIN_WC_DESCRIBE "@DOLPHIN_WC_DESCRIBE@")
set(DOLPHIN_WC_BRANCH "@DOLPHIN_WC_BRANCH@")
set(CMAKE_OSX_DEPLOYMENT_TARGET "@CMAKE_OSX_DEPLOYMENT_TARGET@")

set(INSIDE_CPACK TRUE)
include("${PROJECT_SOURCE_DIR}/CMake/ScmRevGen.cmake")

set(CPACK_PACKAGE_VERSION_MAJOR "${DOLPHIN_VERSION_MAJOR}")
set(CPACK_PACKAGE_VERSION_MINOR "${DOLPHIN_VERSION_MINOR}")
if(DOLPHIN_WC_BRANCH STREQUAL "stable")
  set(CPACK_PACKAGE_VERSION_PATCH "${DOLPHIN_VERSION_PATCH}")
else()
  # Extract commits since last tag
  string(REGEX REPLACE ".+-([0-9]+)-[^-]+((-dirty)?)$" "\\1" DOLPHIN_WC_COMMITS_AHEAD_TAG "${DOLPHIN_WC_DESCRIBE}")
  set(CPACK_PACKAGE_VERSION_PATCH "${DOLPHIN_WC_COMMITS_AHEAD_TAG}")
endif()
set(CPACK_PACKAGE_VERSION "${CPACK_PACKAGE_VERSION_MAJOR}.${CPACK_PACKAGE_VERSION_MINOR}.${CPACK_PACKAGE_VERSION_PATCH}")
set(CPACK_PACKAGE_FILE_NAME "${CPACK_PACKAGE_NAME}-${CPACK_PACKAGE_VERSION}-${CPACK_SYSTEM_NAME}")
